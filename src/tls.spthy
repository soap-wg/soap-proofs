rule Domain:
  []
  -->
  [ !Domain($Domain) ]

rule DomainCompromise:
  [ !Domain($Domain) ]
  --[ CompromisedDomain($Domain)
    , SomeCompromise() ]->
  [ !DomainCompromise($Domain) ]

rule TLSClientOut:
  [ TLSClient_Out(~sess, $Domain, msg), !Domain($Domain) ]
  -->
  [ !TLSClient(~sess, $Domain, msg) ]

rule TLSAdversaryClientOut:
  [ In(~sess), In(msg), !Domain($Domain) ]
  -->
  [ !TLSClient(~sess, $Domain, msg) ]

rule TLSServerOut:
  [ TLSServer_Out(~sess, $Domain, msg), !Domain($Domain) ]
  -->
  [ !TLSServer(~sess, $Domain, msg) ]

rule TLSAdversaryServerOut:
  [ In(~sess), In(msg), !DomainCompromise($Domain) ]
  -->
  [ !TLSServer(~sess, $Domain, msg) ]

rule TLSClientToServer:
  [ !TLSClient(~sess, $Domain, msg) ]
  -->
  [ TLSServer_In(~sess, $Domain, msg) ]

rule TLSClientToAdversaryServer:
  [ !TLSClient(~sess, $Domain, msg)
  , !DomainCompromise($Domain) ]
  -->
  [ Out(msg), Out(~sess) ]

rule TLSServerToClient:
  [ !TLSServer(~sess, $Domain, msg) ]
  -->
  [ TLSClient_In(~sess, $Domain, msg) ]

rule TLSServerToAdversaryClient:
  [ In(~sess)
  , !TLSServer(~sess, $Domain, msg) ]
  -->
  [ Out(msg) ]
